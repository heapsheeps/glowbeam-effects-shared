_Duration("Duration", Float) = 10.0
_GridSize("Grid Size", Range(0.0, 1.0)) = 0.5
_Amplification("Amplification", Range(0.0, 1.0)) = 0.5
_LineThickness("Line Thickness", Range(0.0, 1.0)) = 0.25
_UseNormals("Use Normals", Float) = 1

float4 EffectMain()
{
    float2 uv = LocalUV;

    // set up input parameters
    float gridSize = round(_GridSize * 32.0);
    float amp = 0.2 * _Amplification;
    float t = _Time.y / _Duration;

    // sample normal image
    float3 normal;
    if(_UseNormals > 0.5)
    {
        normal = SAMPLE_TEXTURE2D(_Normals, sampler_Normals, uv).rgb;
    }
    else
    {
        normal = SAMPLE_TEXTURE2D(_EdgeDistance, sampler_EdgeDistance, uv).rrr;
    }

    // set up uv coordinate system
    float2 p = 2.0 * (uv - 0.5) * float2(_ScreenParams.x / _ScreenParams.y, 1.0);
    p.y += t;
    p.x += 0.1 * sin(TWO_PI * t);
    p.x += (((1.0 - normal.x) - 0.5) * 2.0) * amp;
    p.y += ((normal.y - 0.5) * 2.0) * amp;
    p = frac(p * gridSize);

    // create the grid by highlighting only min and max values for p.x and p.y
    float lineWidth = (_LineThickness + 0.02) / 5.0;
    float high = 1.0 - lineWidth / 2.0;
    float highSmooth = 1.0 - lineWidth;
    float low = lineWidth / 2.0;
    float lowSmooth = lineWidth;

    float gridR = smoothstep(highSmooth, high, p.x);
    float gridT = smoothstep(highSmooth, high, p.y);
    float gridL = 1.0 - smoothstep(low, lowSmooth, p.x);
    float gridB = 1.0 - smoothstep(low, lowSmooth, p.y);
    float grid = max(max(max(gridR, gridT), gridL), gridB);

    return float4(grid, grid, grid, grid);
}